
! -----------------------------------------------------------------------
! HTML
!
! HyperText Markup Language
!
! This grammar requires little explanation - being the dominant format used 
! for websites. Like XML, HTML is a descendant of SGML (Standardized 
! General Markup Language).
!
! For more information, visit http://www.w3.org/MarkUp/ 
!
! Note: This is an ad hoc version of the language. If there are any flaws, 
! please visit www.DevinCook.com/GOLDParser
! -----------------------------------------------------------------------

"Name"         = 'HTML'
"Version"      = '1.0'
"Author"       = 'W3C Recommendation' 
"About"        = 'HyperText Markup Language'

"Start Symbol" = <HTML>

{ID Tail}       = {Alphanumeric} + [-/]
{Symbol Chars}  = {Printable} - {Letter} - [<>''"=] - {Whitespace}
{String Chars}  = {Printable} - ["]
{Hex Digit}     = {Digit} + [abcdef]


StringLiteral = '"' {String Chars}* '"'

ID          = {Letter}{ID Tail}*
Symbol      = {Symbol Chars}
CharName    = '&' {Letter}+ ';'
CharNumber  = '&#' {Digit}+ ';'
Color       = '#' {Hex Digit}+

Comment Start = '<!--'
Comment End   = '-->'


! ----------------------------------------------------------- Webpage
<HTML> ::= <DocType Opt> '<html' <Parameters> '>' <Webpage Body> '</html>'              

<DocType Opt> ::= '<!doctype' <Parameters> '>'
                |

<Webpage Body> ::= <Head> <Body>

<Head>  ::= '<head' <Parameters> '>' <Head Items> '</head>'
          |

<Head Items> ::= <Head Item> <Head Items>
               | 

<Head Item>  ::= '<title'     <Parameters> '>' <Content> '</title>'
               | '<meta'      <Parameters> '>' 
               | '<link'      <Parameters> '>' 
               | '<base'      <Parameters> '>'        
               | '<basefont'  <Parameters> '>' 
               | '<script'    <Parameters> '>' <Content> '</script>'

<Body>  ::= '<body' <Parameters> '>' <Content> '</body>'

! ----------------------------------------------------------- Content
! The paragraph tag has special scope since it does not require and end tag

<Content> ::= '<p' <Parameters> '>' <P Content>               
            | <Tag>        <Content>
            | <Word>       <Content>
            |
            
<P Content> ::= <Tag>        <P Content>
              | <Word>       <P Content>
              | 
             

! ----------------------------------------------------------- 

<Parameters> ::= <Parameter> <Parameters>
               |  

<Parameter>  ::= Id '=' StringLiteral
               | Id '=' Color
               | Id '=' Id
               | Id
               | StringLiteral

! ----------------------------------------------------------- Tags
<Tag>   ::= '<img'     <Parameters> '>' 
          | '<br'      <Parameters> '>' 
          | '<hr'      <Parameters> '>'                      
            
          | '<p'           <Parameters> '>' <P Content> '</p>'   
          
          | '<h1'          <Parameters> '>' <Content>   '</h1>'
          | '<h2'          <Parameters> '>' <Content>   '</h2>'
          | '<h3'          <Parameters> '>' <Content>   '</h3>'
          | '<h4'          <Parameters> '>' <Content>   '</h4>'
          | '<h5'          <Parameters> '>' <Content>   '</h5>'
          | '<h6'          <Parameters> '>' <Content>   '</h6>'
          
          | '<a'           <Parameters> '>' <Content>   '</a>'
          | '<map'         <Parameters> '>' <Map items> '</map>'
          
          | '<b'           <Parameters> '>' <Content>   '</b>'          
          | '<u'           <Parameters> '>' <Content>   '</u>'
          | '<em'          <Parameters> '>' <Content>   '</em>'
          | '<i'           <Parameters> '>' <Content>   '</i>'
          | '<strike'      <Parameters> '>' <Content>   '</strike>'
          | '<strong'      <Parameters> '>' <Content>   '</strong>'
          | '<font'        <Parameters> '>' <Content>   '</font>'
                    
          | '<big'         <Parameters> '>' <Content>   '</big>'          
          | '<small'       <Parameters> '>' <Content>   '</small>'
          | '<sup'         <Parameters> '>' <Content>   '</sup>'
          | '<sub'         <Parameters> '>' <Content>   '</sub>'
          
          | '<blockquote'  <Parameters> '>' <Content>   '</blockquote>'
          | '<address'     <Parameters> '>' <Content>   '</address>'
          | '<code'        <Parameters> '>' <Content>   '</code>'
          | '<cite'        <Parameters> '>' <Content>   '</cite>'
          | '<caption'     <Parameters> '>' <Content>   '</caption>'
          | '<samp'        <Parameters> '>' <Content>   '</samp>'
          | '<kbd'         <Parameters> '>' <Content>   '</kbd>' 
          | '<tt'          <Parameters> '>' <Content>   '</tt>'
          
          | '<center'      <Parameters> '>' <Content>   '</center>'          
          | '<span'        <Parameters> '>' <Content>   '</span>'
          | '<div'         <Parameters> '>' <Content>   '</div>'
                    
          | '<applet'      <Parameters> '>' <Params>    '</applet>'
          | '<object'      <Parameters> '>' <Params>    '</object>'
          
          | '<table'       <Parameters> '>' <Table Rows> '</table>'          
          | '<ul'          <Parameters> '>' <List Items> '</ul>' 
          | '<ol'          <Parameters> '>' <List Items> '</ol>'           
          | '<dir'         <Parameters> '>' <List Items> '</dir>'
          | '<menu'        <Parameters> '>' <List Items> '</menu>'
          | '<dl'          <Parameters> '>' <Def Items>  '</dl>' 

          | '<form'        <Parameters> '>' <Content>    '</form>' 
          | '<input'       <Parameters> '>' 
          | '<select'      <Parameters> '>' <Options>    '</select>' 
          | '<textarea'    <Parameters> '>' <Content>    '</textarea>' 



! ============================================== Table Body

<Table Rows> ::= '<tr' <Parameters> '>' <Table Cells> '</tr>' <Table Rows>
               |
               
<Table Cells> ::= <Table Cell> <Table Cells>
                | 
               
<Table Cell>  ::= '<td' <Parameters> '>' <Content> '</td>'
                | '<th' <Parameters> '>' <Content> '</th>' 
             

! ============================================== Definition List

<Def Items> ::= <Def Item> <Def Items>
              |
               
<Def Item> ::= '<dt' <Parameters> '>' <Content> '</dt>'
             | '<dd' <Parameters> '>' <Content> '</dd>'


! ============================================== List Body

<List Items> ::= <List Item> <List Items>
               |
               
<List Item> ::= '<li' <Parameters> '>' <Content> '</li>'               


! ============================================== Select Items

<Options> ::= <Option> <Options>
            | 

<Option> ::= '<option' <Parameters> '>'


! ============================================== Map Items

<Map Items> ::= <Area> <Map Items>
              | 

<Area> ::= '<area' <Parameters> '>'


! ============================================== Object Body

<Params> ::= '<param' <Parameters> '>'  <Params>
           |

! ========= Valid words include all the reserved symbols.

<Word> ::= Id
         | Symbol
         | '='
         | ''
         | CharName
         | CharNumber
         | Color



